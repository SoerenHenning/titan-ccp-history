buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
}

plugins {
  id "com.github.spotbugs"
}

apply plugin: 'application'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'eclipse'


sourceCompatibility = "1.11"
targetCompatibility = "1.11"

dependencies {
    implementation project(':')

    implementation 'org.slf4j:slf4j-simple:1.6.1'
    implementation 'redis.clients:jedis:2.9.0'

    implementation('org.industrial-devops:titan-ccp-common-cassandra:0.0.2-SNAPSHOT') { changing = true }
    implementation('org.industrial-devops:titan-ccp-common:0.1.0-SNAPSHOT') { changing = true }

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

mainClassName = "titan.ccp.history.recordcounter.RecordCounter"

pmd {
  ruleSets = [] // Gradle requires to clean the rule sets first
  ruleSetFiles = files("../config/pmd.xml")
  ignoreFailures = false
  toolVersion = "6.7.0"
}

checkstyle {
  configDirectory = file("../config")
  configFile = file("../config/checkstyle.xml")
  maxWarnings = 0
  ignoreFailures = false
  toolVersion = "8.12"
}

spotbugs {
  excludeFilter = file("../config/spotbugs-exclude-filter.xml")
  reportLevel = "low"
  effort = "max"
  ignoreFailures = false
  toolVersion = '3.1.7'
}

// Per default XML reports for SpotBugs are generated
// Include this to generate HTML reports
tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
  reports {
    // Either HTML or XML reports can be activated
    html.enabled true
    xml.enabled false
  }
}

task checkstyle {
  group 'Quality Assurance'
  description 'Run Checkstyle'

  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
}

task pmd {
  group 'Quality Assurance'
  description 'Run PMD'

  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
}

task spotbugs {
  group 'Quality Assurance'
  description 'Run SpotBugs'

  dependsOn 'spotbugsMain'
  dependsOn 'spotbugsTest'
}

eclipse {
    classpath {
       downloadSources=true
       downloadJavadoc=true
    }
}
